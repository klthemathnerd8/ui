==Problem==
A bowl is formed by attaching four regular hexagons of side $1$ to a square of side $1$. The edges of the adjacent hexagons coincide, as shown in the figure. What is the area of the octagon obtained by joining the top eight vertices of the four hexagons, situated on the rim of the bowl?
<asy>
import three;
size(225);
currentprojection=
  orthographic(camera=(-5.52541796301147,-2.61548797564715,1.6545450372312),
               up=(0.00247902062334861,0.000877141782387748,0.00966536329192992),
               target=(0,0,0),
               zoom=0.570588560870951);
currentpen = black+1.5bp;
triple A = O;
triple M = (X+Y)/2;
triple B = (-1/2,-1/2,1/sqrt(2));
triple C = (-1,0,sqrt(2));
triple D = (0,-1,sqrt(2));
transform3 rho = rotate(90,M,M+Z);

//arrays of vertices for the lower level (the square), the middle level,
//and the interleaves vertices of the upper level (the octagon)
triple[] lVs = {A};
triple[] mVs = {B};
triple[] uVsl = {C};
triple[] uVsr = {D};

for(int i = 0; i < 3; ++i){
  lVs.push(rho*lVs[i]);
  mVs.push(rho*mVs[i]);
  uVsl.push(rho*uVsl[i]);
  uVsr.push(rho*uVsr[i]);
}

lVs.cyclic = true;
uVsl.cyclic = true;

for(int i : new int[] {0,1,2,3}){
  draw(uVsl[i]--uVsr[i]);
  draw(uVsr[i]--uVsl[i+1]);
}
draw(lVs[0]--lVs[1]^^lVs[0]--lVs[3]);
for(int i : new int[] {0,1,3}){
  draw(lVs[0]--lVs[i]);
  draw(lVs[i]--mVs[i]);
  draw(mVs[i]--uVsl[i]);
}
for(int i : new int[] {0,3}){
  draw(mVs[i]--uVsr[i]);
}

for(int i : new int[] {1,3}) draw(lVs[2]--lVs[i],dashed);
draw(lVs[2]--mVs[2],dashed);
draw(mVs[2]--uVsl[2]^^mVs[2]--uVsr[2],dashed);
draw(mVs[1]--uVsr[1],dashed);

//Comment two lines below to remove red edges
//draw(lVs[1]--lVs[3],red+2bp);
//draw(uVsl[0]--uVsr[0],red+2bp);
</asy>
$\textbf{(A) }6\qquad\textbf{(B) }7\qquad\textbf{(C) }5+2\sqrt{2}\qquad\textbf{(D) }8\qquad\textbf{(E) }9$
